plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.recipemate'
version = '0.0.1-SNAPSHOT'
description = 'Recipe-based smart grocery group buying platform for students'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencyManagement {
    dependencies {
        dependency 'org.apache.commons:commons-lang3:3.18.0'
    }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.retry:spring-retry'
	implementation 'org.springframework:spring-aspects'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
	
	// Redis and Cache dependencies
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	
	// Image optimization dependency
	implementation 'net.coobird:thumbnailator:0.4.19'
	
	// Cloudinary for image upload
    implementation 'com.cloudinary:cloudinary-http5:2.3.0'

	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // QueryDSL dependencies
    def queryDslVersion = "7.0"
    implementation "io.github.openfeign.querydsl:querydsl-jpa:${queryDslVersion}"
    annotationProcessor "io.github.openfeign.querydsl:querydsl-apt:${queryDslVersion}:jakarta"
}

def generatedDir = layout.buildDirectory.dir("generated/querydsl")

tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(file(generatedDir))
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('bootRun') {
	systemProperty 'spring.profiles.active', 'dev'
	
	// .env 파일에서 환경 변수 자동 로드
	def envFile = file('.env')
	if (envFile.exists()) {
		envFile.readLines().each { line ->
			def trimmedLine = line.trim()
			// 주석(#)과 빈 줄 무시
			if (!trimmedLine.isEmpty() && !trimmedLine.startsWith('#')) {
				def parts = trimmedLine.split('=', 2)
				if (parts.length == 2) {
					def key = parts[0].trim()
					def value = parts[1].trim()
					environment key, value
				}
			}
		}
	}
}

sourceSets {
    main.java.srcDirs += [ generatedDir ]
}

clean {
    delete file(generatedDir)
}